IF
Die()
THEN
RESPONSE #100
CreateVisualEffectObject("SPHOLY",Myself)
ActionOverride(Player1,DisplayStringHead(Myself,@1007))
DropInventory()
Wait(2)
LeaveParty()
ApplySpellRES("SPWI055",Myself) 
DestroySelf()
END	

IF
Global("InParty","LOCALS",0)
InParty("Tamoko")  
THEN
RESPONSE #100
SetGlobal("InParty","LOCALS",1)
SetGlobal("TamokoJoin","GLOBAL",1)
ChangeAIScript("DPlayer2",DEFAULT)
MakeGlobal()
END

IF
GlobalLT("Swo_Desease","Locals",2)
HPPercentLT(Myself,30)
THEN
RESPONSE #100
SetGlobal("Swo_Desease","Locals",2)
StartDialogueNoSet(Player1)
END

IF
AreaCheck("%Undercity%")
Range("Door0125",90)
Global("Swo_TamokInt","Global",0)
THEN
RESPONSE #100
SetGlobal("Swo_TamokInt","Global",1)
END

IF
AreaCheck("bd0120")
Global("Swo_TamokInt","Global",2)
OR(2)
Range("Sarcophagus2",40)
Range("TranBD0130",40)
THEN
RESPONSE #100
SetGlobal("Swo_TamokInt","Global",3)
END

IF
GlobalGT("Chapter","Global",9)
!StateCheck(Myself,STATE_DISEASED)
OR(2)
See("BDInfec6")
See("BDPCrus")
GlobalLT("Swo_Desease","Locals",2)
Delay(15)
THEN
RESPONSE #100
SetGlobal("Swo_Desease","Locals",1)
ApplySpellRES("sworot",Myself)
END

IF
Global("Chapter","Global",12)
AreaCheck("BD4700")
Dead("BDbelhif")
!StateCheck(Myself,STATE_DISEASED)
GlobalLT("Swo_Desease","Locals",2)
Delay(15)
THEN
RESPONSE #100
SetGlobal("Swo_Desease","Locals",1)
ApplySpellRES("sworot",Myself)
END

IF
AreaCheck("bd0100")
Global("Swo_TamokInt","Global",4)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("Swo_TamokInt","Global",5)
END

IF
AreaCheck("bd0130")
OR(3)
Range("Bane_mural",30)
Range("Trap_miscast_magic",30)
Range("Sarcophagus_bane_room",30)
Global("Swo_Sareroom","BD0130",0)
THEN
RESPONSE #100
SetGlobal("Swo_Sareroom","BD0130",1)
END

IF
AreaCheck("bd7100")
Global("Swo_TamokInt","Global",6)
OR(2)
Range("Glint Romance 1",50)
Range("Corwin Romance 2",50)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("Swo_TamokInt","Global",7)
END

IF
OR(2)
Global("Chapter","Global",8)
Global("Chapter","Global",9)
Global("Swo_SkieReact","Locals",0)
See("BDSkie")
THEN
RESPONSE #100
SetGlobal("Swo_SkieReact","Locals",1)
END

IF
Global("Swo_SkieReact","Locals",1)
!See("BDSkie")
THEN
RESPONSE #100
SetGlobal("Swo_SkieReact","Locals",2)
END

IF
GlobalGT("Chapter","Global",7)
InParty("CVSandr")
Global("SanHealTamok","LOCALS",0)
InParty(Myself)
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
SetGlobal("SanHealTamok","LOCALS",1)
END

IF
Global("SanHealTamok","LOCALS",2)
InParty("CVSandr")
InParty(Myself)
OR(3)
AreaCheck("BD5000")
AreaCheck("BD7300")
AreaCheck("BD7200")
Delay(90)
THEN
RESPONSE #100
SetGlobal("SanHealTamok","LOCALS",3)
END

IF
PartyRested()
Global("Swo_SkieReact","Locals",3)
GlobalGT("Chapter","Global",8)
THEN
RESPONSE #100
SetGlobal("Swo_SkieReact","Locals",4)
END

IF
PartyRested()
GlobalGT("Chapter","Global",9)
Global("Swo_camp","LOCALS",0)
AreaType(OUTDOOR)
OR(2)
Global("Swo_SkieReact","Locals",5)
GlobalLT("Swo_SkieReact","Locals",2)
THEN
RESPONSE #100
DayNight(MIDNIGHT)
SetGlobal("Swo_camp","LOCALS",1)
END

IF
Global("Swo_Madele","Global",1)
AreaType(OUTDOOR)
Delay(80)
THEN
RESPONSE #100
SetGlobal("Swo_Madele","Global",2)
END

IF
Global("Chapter","Global",12)
Global("Swo_InJail","Global",0)
AreaCheck("BD4300")
InParty(Myself)
Dead("BDbelhif")
THEN
RESPONSE #100
SetGlobal("Swo_InJail","Global",1)
END

IF
Global("Swo_InJail","Global",2)
AreaCheck("bd0104")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetDialogue("TamokoJ")
Wait(1)
StartDialogNoSet(Player1)
END

IF
OR(12)
Global("Swo_TamokInt","Global",1)
Global("Swo_TamokInt","Global",3)
Global("Swo_TamokInt","Global",5)
Global("Swo_TamokInt","Global",7)
Global("Swo_SkieReact","Locals",2)
Global("SanHealTamok","LOCALS",1)
Global("SanHealTamok","LOCALS",3)
Global("Swo_SkieReact","Locals",4)
Global("Swo_Madele","Global",2)
Global("Swo_camp","LOCALS",1)
Global("Swo_Desease","Locals",2)
Global("Swo_Sareroom","BD0130",1)
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
See(Player1)
THEN
RESPONSE #100
StartDialogNoSet(Player1)
END